plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'

    // Lint
    id "com.diffplug.spotless" version "6.25.0" apply false
}

repositories {
    mavenCentral()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    group = 'com.lalala'
    version = '0.0.1-SNAPSHOT'

    java {
        sourceCompatibility = '17'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        // AOP
        implementation "org.springframework.boot:spring-boot-starter-aop"

        // Lombok
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
    }

    spotless {
        java {
            googleJavaFormat()			// 구글 자바 포맷 적용

            removeUnusedImports()       // 사용하지 않는 import 제거
            trimTrailingWhitespace() 	// 공백 제거
            endWithNewline() 			// 끝부분 New Line 처리

            // Indentation
            indentWithTabs(2)
            indentWithSpaces(4)

            // import 순서 정의
            importOrder(
                    "java",
                    "javax",
                    "lombok",
                    "org.springframework",
                    "",
                    "\\#",
                    "org.junit",
                    "\\#org.junit",
            )
        }
    }
}

project(':common-module:common-mvc') {
    dependencies {
        implementation project(':common-module:common')
    }
}

project(':common-module:common-reactive') {
    dependencies {
        implementation project(':common-module:common')
    }
}

test {
    useJUnitPlatform()
}

clean {
    delete 'src/generated'
}
