plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
}

repositories {
	mavenCentral()
}

bootJar {
	enabled = false
}
jar {
	enabled = true
}

subprojects {
	if (project.name.startsWith("common")) {
		return
	}

	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.lalala'
	version = '0.0.1-SNAPSHOT'

	java {
		sourceCompatibility = '17'
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		implementation project(":common-module:common")
		implementation project(":common-module:common-mvc")

		// spring mvc
		implementation 'org.springframework.boot:spring-boot-starter-web'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'

		// lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// Eureka
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:4.1.0'

		// OpenFeign
		implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.1.0'

		// Actuator
		implementation 'org.springframework.boot:spring-boot-starter-actuator'

		// Open Tracing
		implementation 'io.micrometer:micrometer-tracing-bridge-brave'
		implementation 'io.zipkin.reporter2:zipkin-reporter-brave'

		// Logback for LogStash
		implementation 'net.logstash.logback:logstash-logback-encoder:6.3'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}
