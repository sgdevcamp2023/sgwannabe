plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'

	// Lint
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.lalala'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// apple chip 에서 발생하는 dns 조회 오류 해결
	def isMacOS = System.getProperty('os.name').startsWith('Mac OS X')
	def architecture = System.getProperty('os.arch').toLowerCase()
	if (isMacOS && architecture == 'aarch64') {
		implementation('io.netty:netty-resolver-dns-native-macos:4.1.90.Final:osx-aarch_64')
	}

	// Eureka
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'

	// Actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	java {
		googleJavaFormat()			// 구글 자바 포맷 적용

		removeUnusedImports()       // 사용하지 않는 import 제거
		trimTrailingWhitespace() 	// 공백 제거
		endWithNewline() 			// 끝부분 New Line 처리

		// Indentation
		indentWithTabs(2)
		indentWithSpaces(4)

		// import 순서 정의
		importOrder(
				"java",
				"javax",
				"lombok",
				"org.springframework",
				"",
				"\\#",
				"org.junit",
				"\\#org.junit",
		)
	}
}
